CREATE OR ALTER PROCEDURE [dbo].[NSP_SALESREPORT_ITEMWISE_DETAIL]
--DECLARE 
	@DATE1 DATETIME,
	@DATE2 DATETIME,
	@DIV VARCHAR(3) = '%',
	@MC VARCHAR(25) = '%',
	@BC VARCHAR(MAX)= '%',	
	@BARCODEWISEREPORT TINYINT = 0,
	@BARCODEDETAIL TINYINT =0,
	@DIVISIONWISEREPORT TINYINT = 0,
	@WISE VARCHAR(25) = 'BILLWISE',
	@SHOWINTERCOMPANYONLY TINYINT = 0,
	@PTYPE INT = 100,
	@FLAG TINYINT = 0 ---0 filter by TRNDATE |1 filter by DAYSTARTDATE
AS

/*
set  @DATE1='2024-01-01' ; set @DATE2='2024-05-31' ; set @DIV=N'%' ; set @MC=N'%' ; set @BC=N'%' ; set @PTYPE=N'100' ;  set @WISE=N'BILLWISE' ;
SET @FLAG = 1
*/

set nocount on

DECLARE @SHOWBARCODEDETAIL TINYINT=1
DECLARE @BCDETAIL_QUERY VARCHAR(500)
DECLARE  @BARCODECOMPULOSRYMODE TINYINT = 0

SELECT @BCDETAIL_QUERY = 'SELECT BARCODE,COLOR,SIZE,MCODE FROM BARCODE_DETAIL',@SHOWBARCODEDETAIL = ISNULL(EnableBarcodeDetails,0),@BARCODECOMPULOSRYMODE = ENABLEBARCODECOMPULSORY 
FROM SETTING


DROP TABLE IF EXISTS #MENUITEM

SELECT MCODE , MENUCODE , DESCA , BASEUNIT
INTO #MENUITEM
FROM MENUITEM
WHERE (@PTYPE = 100 OR PTYPE = @PTYPE) AND MCODE LIKE @MC


DROP TABLE IF EXISTS #MAIN

SELECT A.VoucherType , A.VCHRNO , A.CHALANNO , A.REFORDBILL , A.REFBILL , 
IIF(@FLAG=1, D.DAYDATE, A.TRNDATE) TRNDATE , DM.MITI BSDATE , A.DIVISION , 
IIF( @DIVISIONWISEREPORT = 1 , Y.NAME , '' ) DIVISION_NAME , A.PhiscalID , T.[NAME] Terminal , A.TRNAC , A.BILLTO , 
ISNULL(A.BILLTOMOB, A.MEMBERNO) MOBILENO , A.TRNMODE , A.Stamp , A.TRNTIME , A.TRNUSER
INTO #MAIN
FROM RMD_TRNMAIN A
LEFT JOIN vwSessionDay D ON A.[SHIFT] = D.SESSIONID AND A.DIVISION = D.DIVISION AND A.PhiscalID = D.PHISCALID
LEFT JOIN DATEMITI DM ON IIF(@FLAG=1, D.DAYDATE,	A.TRNDATE) = DM.AD
LEFT JOIN DIVISION Y ON A.DIVISION = Y.INITIAL
LEFT JOIN SALESTERMINAL T ON A.TERMINAL = T.INITIAL
WHERE ((@SHOWINTERCOMPANYONLY=0 AND A.VOUCHERTYPE IN ('SI','TI','CN','RE')) OR (@SHOWINTERCOMPANYONLY=1 AND A.VOUCHERTYPE IN ('IC','IR')))
AND	IIF(@FLAG=1, D.DAYDATE,	A.TRNDATE) BETWEEN @DATE1 AND @DATE2 AND A.DIVISION LIKE @DIV


DROP TABLE IF EXISTS #REPDATA

SELECT A.MOBILENO, S.NAME SALESMAN, X.MENUCODE ITEMCODE, B.ITEMDESC, B.MCODE,A.VCHRNO,A.CHALANNO,A.REFORDBILL,A.REFBILL,A.TRNDATE, A.BSDATE,A.DIVISION,
IIF( TRNMODE = 'CREDIT' , Z.ACNAME , IIF( ISNULL(BILLTO,'') <> '' , BILLTO , Z.ACNAME ) ) BILLTO,
B.BC BARCODE, B.ALTUNIT BILLUNIT,IIF( A.VOUCHERTYPE IN ('SI','TI','IC') , AltQty , ALTQTY_IN ) BILLQTY,B.ALTRATE BILLRATE, 
X.BASEUNIT BASEUNIT, IIF( A.VOUCHERTYPE IN ('SI','TI','IC') , REALQTY , REALQTY_IN ) BASEQTY,REALRATE BASERATE,
IIF( A.VOUCHERTYPE IN ('CN','RE','IR') , B.AMOUNT*-1 , B.AMOUNT ) AMOUNT,
IIF( A.VOUCHERTYPE IN ('CN','RE','IR') , B.DISCOUNT*-1 , B.DISCOUNT ) DISCOUNT,
IIF( A.VOUCHERTYPE IN ('CN','RE','IR') , B.SERVICETAX *-1 , B.SERVICETAX ) SCHARGE,
IIF( A.VOUCHERTYPE IN ('CN','RE','IR') , (B.TAXABLE+B.NONTAXABLE) *-1 , (B.TAXABLE+B.NONTAXABLE) ) NETSALE,
IIF( A.VOUCHERTYPE IN ('CN','RE','IR') , B.TAXABLE*-1 , B.TAXABLE ) TAXABLE,
IIF( A.VOUCHERTYPE IN ('CN','RE','IR') , B.NONTAXABLE*-1 , B.NONTAXABLE ) NONTAXABLE,
IIF( A.VOUCHERTYPE IN ('CN','RE','IR') , B.VAT*-1 , B.VAT ) VAT,
IIF( A.VOUCHERTYPE IN ('CN','RE','IR') , (B.TAXABLE+B.NONTAXABLE+B.VAT)*-1 , (B.TAXABLE+B.NONTAXABLE+B.VAT) ) NETAMNT,
A.DIVISION_NAME,A.STAMP,A.TRNUSER,A.TRNTIME, TPS.StartTime, TPS.EndTime, A.Terminal
INTO #REPDATA
FROM #MAIN A 
INNER JOIN RMD_SALESPROD B ON A.VCHRNO = B.VCHRNO AND A.DIVISION = B.DIVISION AND ISNULL(A.PhiscalID,'') = ISNULL(B.PHISCALID,'')
INNER JOIN #MENUITEM X ON B.MCODE = X.MCODE 
LEFT JOIN RMD_TRNPROD_STATUS TPS ON B.VCHRNO = TPS.VCHRNO AND B.MCODE = TPS.MCODE AND B.SNO = TPS.SNO
LEFT JOIN RMD_ACLIST Z ON A.TRNAC = Z.ACID 
LEFT JOIN SALESMAN S ON S.SALESMANID = B.SALESMANID
WHERE ((@BARCODEWISEREPORT = 1 AND ISNULL(B.BC,'') LIKE @BC) OR (@BARCODEWISEREPORT = 0 AND ISNULL(B.BC,'') LIKE '%'))


BEGIN

	IF @WISE = 'BILLWISE'
	BEGIN

		IF @BARCODEDETAIL = 0
			
			SELECT TRNDATE,BSDATE,VCHRNO,REFBILL REFNO, ITEMCODE, ITEMDESC DESCA , BILLTO , BARCODE , BILLUNIT , BILLQTY , BILLRATE , BASEUNIT , BASEQTY , BASERATE , AMOUNT , DISCOUNT , SCHARGE , NETSALE , TAXABLE , NONTAXABLE , VAT , NETAMNT , TRNUSER , TRNTIME , B.NAME DIVISION, SALESMAN, MOBILENO, StartTime, EndTime, TERMINAL
			FROM #REPDATA A 
			LEFT JOIN DIVISION B ON A.DIVISION = B.INITIAL 
			ORDER BY TRNDATE,A.STAMP

		ELSE IF @BARCODEDETAIL = 1
		BEGIN

			DECLARE @SQL VARCHAR(MAX)

			SET @SQL = N'
						SELECT A.TRNDATE,A.BSDATE,A.VCHRNO,A.REFBILL REFNO,A.ITEMDESC DESCA , A.BILLTO , A.BILLUNIT , A.BILLQTY , A.BILLRATE , A.BASEUNIT , A.BASEQTY , A.BASERATE , A.AMOUNT,A.DISCOUNT,A.SCHARGE,A.NETSALE , A.TAXABLE , A.NONTAXABLE , A.VAT , A.NETAMNT , B.*,A.TRNUSER,A.TRNTIME,A.DIVISION,A.SALESMAN, A.MOBILENO 
						FROM #REPDATA A 
						LEFT JOIN (' + @BCDETAIL_QUERY + ') B ON A.MCODE = B.MCODE AND A.BARCODE = B.BARCODE
						ORDER BY A.TRNDATE,A.STAMP'					

			EXEC(@SQL)

		END

	END
	ELSE IF @WISE = 'DAYWISE'

		IF @BARCODEDETAIL = 0

			SELECT TRNDATE,BSDATE,X.MENUCODE,X.DESCA,IIF(@BARCODEWISEREPORT = 1 , A.BARCODE , '' ) BARCODE,X.BASEUNIT UNIT ,SUM(BASEQTY) BASEQTY,SUM(AMOUNT) AMOUNT ,SUM(DISCOUNT)DISCOUNT ,SUM(SCHARGE) SCHARGE ,SUM(NETSALE) NETSALE ,SUM(TAXABLE)TAXABLE ,SUM(NONTAXABLE) NONTAXABLE , SUM(A.VAT) VAT ,SUM(NETAMNT)NETAMNT,DIVISION_NAME, A.SALESMAN
				FROM #REPDATA A 
				INNER JOIN #MENUITEM X ON A.MCODE = X.MCODE 
				GROUP BY TRNDATE,DIVISION_NAME,BSDATE,X.MENUCODE,X.DESCA,X.BASEUNIT,IIF(@BARCODEWISEREPORT = 1 , A.BARCODE , '' ), SALESMAN 
				ORDER BY TRNDATE,X.DESCA

		ELSE
		BEGIN

			DROP TABLE IF EXISTS #REPDATA_1

			SELECT TRNDATE,BSDATE,X.MENUCODE,X.DESCA,A.BARCODE,X.BASEUNIT UNIT,SUM(BASEQTY) BASEQTY,SUM(AMOUNT) AMOUNT,SUM(DISCOUNT)DISCOUNT,SUM(SCHARGE) SCHARGE,
			SUM(NETSALE) NETSALE,SUM(TAXABLE)TAXABLE,SUM(NONTAXABLE) NONTAXABLE, SUM(A.VAT) VAT,SUM(NETAMNT)NETAMNT,DIVISION_NAME,X.MCODE
			INTO #REPDATA_1 
			FROM #REPDATA A 
			INNER JOIN #MENUITEM X ON A.MCODE = X.MCODE 
			GROUP BY TRNDATE,BSDATE,X.MENUCODE,X.DESCA,X.BASEUNIT,A.BARCODE,DIVISION_NAME,X.MCODE

			DECLARE @SQL1 VARCHAR(MAX)

			SET @SQL1 = N'
						SELECT A.TRNDATE,A.BSDATE,A.MENUCODE,A.DESCA,A.UNIT,A.BASEQTY,A.AMOUNT,A.DISCOUNT,A.SCHARGE,A.NETSALE,A.TAXABLE,A.NONTAXABLE,A.VAT,A.NETAMNT,B.*,A.DIVISION_NAME 
						FROM #REPDATA_1 A 
						LEFT JOIN (' + @BCDETAIL_QUERY + ') B ON A.MCODE = B.MCODE AND A.BARCODE = B.BARCODE
						ORDER BY A.TRNDATE'

			EXEC(@SQL1)

		END

	END

DROP TABLE IF EXISTS #MAIN
DROP TABLE IF EXISTS #REPDATA
DROP TABLE IF EXISTS #REPDATA_1

set nocount off